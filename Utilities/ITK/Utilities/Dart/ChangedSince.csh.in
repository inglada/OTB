#! /bin/tcsh -f
#
# Find all files changed between two dates
# Usage: ChangedSince.csh StartDate EndDate
#

if ($#argv != 2) then
    echo "Usage: ChangedSince.csh StartDate EndDate"
    exit (1)
endif

echo "Changes from $1 to $2"

set FROM="$1"
set TO="$2"

set TEMPDIR=@ITK_BINARY_DIR@/Testing/Temporary

(cd @ITK_SOURCE_DIR@; cvs log -d"$FROM<$TO" >${TEMPDIR}/$$.allchanges)

gawk -f @ITK_SOURCE_DIR@/Utilities/Dart/ChangedSince.gawk ${TEMPDIR}/$$.allchanges | cut -d"/" -f5- >${TEMPDIR}/$$.newandremoved

echo "This release covers the period ${FROM} through ${TO}" > ${TEMPDIR}/$$.wikitemp
echo "== Changes in this Release ==" >> ${TEMPDIR}/$$.wikitemp
echo "<pre>" >> ${TEMPDIR}/$$.wikitemp
cat ${TEMPDIR}/$$.newandremoved >> ${TEMPDIR}/$$.wikitemp
echo "</pre>" >> ${TEMPDIR}/$$.wikitemp

#
# remove consecutive blank lines
#
cat -s ${TEMPDIR}/$$.wikitemp > ${TEMPDIR}/ChangedSince.txt
echo "A Wiki-compatible list of changes to files is in ${TEMPDIR}/ChangedSince.txt"
#
# cleanup
#
rm -f ${TEMPDIR}/$$.*
